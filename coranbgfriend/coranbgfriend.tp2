/*
 * "Coran's BG Extended Friendship Talks"
 * Version 4 Combined Tutu/BGT/BGEE installer
 * Domi Sotto, updated by berelinde
 * 2015-01-16
 * BGEE compatibility update v4 by AstroBryGuy
*/

/* Backup folder */
BACKUP ~coranbgfriend/backup~

AUTHOR ~Domi Sotto, domi@gibberlings3.net~

/* enable all error messages; nothing suppressed. comment this out for release version */
//MODDER

VERSION ~v4-PR-20150116~

README ~coranbgfriend/readme-coranbg.html~

ALWAYS
  ACTION_IF GAME_IS ~tutu tutu_totsc~ THEN BEGIN
    /* Tell the player it is using Tutu stuff */
    PRINT @0
    /* shutdown of D and BAF after BG content */
    OUTER_SPRINT ~BGT_VAR~ ~~
    /* Platform-Specific Filenames */
    OUTER_SPRINT "CORAN_JOINED" "_CORANJ"
    OUTER_SPRINT "CORAN_BANTER" "_BCORAN"
    OUTER_SPRINT "GARRICK_BANTER" "_BGARRI"
    OUTER_SPRINT "CORAN_BCS" "_CORAN"
  END

  ACTION_IF GAME_IS ~bgt~ THEN BEGIN
    /* Tell the player it is using BGT stuff */
    PRINT @1
    /* shutdown of D and BAF after BG content */
    OUTER_SPRINT ~BGT_VAR~ ~!Global("endofbg1","GLOBAL",2)~
    /* Platform-Specific Filenames */
    OUTER_SPRINT "CORAN_JOINED" "CORANJ"
    OUTER_SPRINT "CORAN_BANTER" "BCORAN"
    OUTER_SPRINT "GARRICK_BANTER" "BGARRI"
    OUTER_SPRINT "CORAN_BCS" "CORAN"
  END
  
  ACTION_IF GAME_IS ~bgee~ THEN BEGIN
    /* Tell the player it is using BGEE stuff */
    PRINT @4
    /* shutdown of D and BAF after BG content */
    OUTER_SPRINT ~BGT_VAR~ ~~
    /* Platform-Specific Filenames */
    OUTER_SPRINT "CORAN_JOINED" "CORANJ"
    OUTER_SPRINT "CORAN_BANTER" "BCORAN"
    OUTER_SPRINT "GARRICK_BANTER" "BGARRI"
    OUTER_SPRINT "CORAN_BCS" "CORAN"
    /* Convert .tra file character sets to UTF-8 for BGEE */
    ACTION_DEFINE_ARRAY p#noconvert BEGIN setup END
  	ACTION_DEFINE_ARRAY p#reload BEGIN END
  	LAF HANDLE_CHARSETS
    	INT_VAR
      		infer_charset = 1
    	STR_VAR
      		tra_path = EVAL ~%MOD_FOLDER%/tra~
      		noconvert_array = p#noconvert
      		reload_array = p#reload
  	END
  END

  ACTION_IF !(GAME_IS ~tutu tutu_totsc bgt bgee~) THEN BEGIN
    /* Tell the player it is not Tutu, BGT, or BGEE */
    FAIL @2
  END
    
END

/* Language Settings */
AUTO_TRA ~coranbgfriend/tra/%s~
LANGUAGE ~English~ ~english~ ~coranbgfriend/tra/english/setup.tra~
LANGUAGE ~Deutsch~ ~german~ ~coranbgfriend/tra/german/setup.tra~
//LANGUAGE ~Russian~ ~russian~ ~coranbgfriend/tra/russian/setup.tra~
//LANGUAGE ~Polish~ ~polish~ ~coranbgfriend/tra/polish/setup.tra~
//LANGUAGE ~Spanish~ ~spanish~ ~coranbgfriend/tra/spanish/setup.tra~
//LANGUAGE ~French~ ~french~ ~coranbgfriend/tra/french/setup.tra~

BEGIN @3

  /* Tells other mods BG1NPC Core is installed */
  COPY ~coranbgfriend/p#component.xx~ ~override/p#coranfriend.g3~

  /* STATE.IDS patching to ToB, courtesy CamDawg */
  /* adds custom IsValidForPartyDialogue state */
  APPEND ~STATE.IDS~ ~0x80101FEF CD_STATE_NOTVALID~ UNLESS ~CD_STATE_NOTVALID~

  /* dialogue */
  COMPILE EVALUATE_BUFFER ~coranbgfriend/dlg/p#coranft.d~

  /* scripts */
  EXTEND_TOP ~%CORAN_BCS%.bcs~ ~coranbgfriend/baf/p#coranbgfriendship.baf~
      EVALUATE_BUFFER

  ACTION_IF NOT FILE_EXISTS_IN_GAME ~x#bg1npccore.g3~ THEN BEGIN
    EXTEND_TOP ~%CORAN_BCS%.bcs~ ~coranbgfriend/baf/p#corannobg1npc.baf~
      EVALUATE_BUFFER
  END

  /* cutscenes */
  COMPILE ~coranbgfriend/baf/p#coweek.baf~
  COMPILE ~coranbgfriend/baf/p#cowee1.baf~
